label a1s2:
"Em nosso caminho para o Sightseer, olho para a floresta e é então que me lembro do que aconteceu antes de eu adormecer."
m "\"Espera, você sabia que Cass e Alex estão vindo?\""
show ami embarrassed c n with dis
a "\"Ah, sim, isso foi uma adição de última hora ao plano. Eu não sabia se o horário de Cassius estaria livre quando partíssemos, e acabou que estava.\""
m "\"Digo, tudo bem, mas acho estranho estarmos trazendo o cara que tentou matar todo mundo.\""
"Estou exagerando um pouco, mas o plano parcialmente bem-sucedido de Alex para desestabilizar Adastra é uma das principais razões pelas quais a vida aqui foi tão difícil."
stop music fadeout 8.0
play background "sfx/lakesounds.ogg" fadein 8.0
show ami serious c n with dis
a "\"Espere um momento, como você ficou sabendo? Eu reservei uma suíte para eles na cidade. Eles devem nos encontrar na universidade.\""
"Franzo a testa."
m "\"Neferu te contou sobre eu nadar até a ilha, mas não sobre como pegamos Alex construindo um dispositivo de comunicação de novo?\""
show ami surprised c n with dis
a "\"Não...\""
m "\"Cara, o Neferu tem prioridades estranhas ultimamente.\""
a "\"Espere, o que é isso sobre o Alex construindo um dispositivo de comunicação?\""
m "\"Bem, ouvimos Cassius falando sobre construir um dispositivo de planta-tecnologia. E Alex construiu um aqui da última vez porque os sinais estavam fora do alcance para interceptação, certo?\""
show ami angry n with dis
a "\"Ah, aquele merdinha! Eu--\""
show ami disappointed eyes n with dis
"Amicus para de repente e respira fundo."
"É como se ele estivesse ponderando profundamente sobre algo."
show ami serious c n with dis
a "\"Talvez eu realmente tenha que matá-lo.\""
m "\"O quê!?\""
"Amicus continua caminhando em direção ao Sightseer, embora seu humor alegre de antes tenha desaparecido completamente."
a "\"Não acredito que ele me faça fazer isso depois que eu dei a ele uma segunda chance de vida imerecida.\""
m "\"Espera, você está falando sério? Vai executá-lo?\""
"De repente, a ideia de que as ações de Alex poderiam realmente levar a consequências mortais é, bem, perturbadora."
"Faria sentido, mas simplesmente não consigo imaginar ele sendo morto assim, com o Amicus sendo o responsável."
"Mas então Amicus suspira novamente."
show ami serious n with dis
a "\"Não. Provavelmente eu deveria. Sei que meu pai teria feito, e como imperador, sou obrigado a buscar justiça para o meu povo--\""
show ami flustered n with dis
a "\"--o que significaria o fim daquele gato e qualquer relação com Omorfa... Mas estou mudando nossa diplomacia para um estilo mais... quêmio, e executar diplomatas, por mais justificado que seja, simplesmente não é como se faz.\""
a "\"As relações com os gatos já estão tensas como estão.\""
m "\"Acho que você deveria apenas deportá-lo.\""
show ami serious c n
a "\"Ah, eu faria isso, você sabe, mas é ainda mais complicado com Cassius envolvido.\""
"Amicus finalmente me coloca no Sightseer e começamos a flutuar pelo lago."
stop background fadeout 5.0
play music "music/memories.ogg" fadein 5.0
m "\"Então, eu pensei que ele estava fazendo trabalho social nas cidades e vilas mais pobres?\""
a "\"Conseguimos sincronizar nossos horários em Lux, então ele nos acompanhará apenas na primeira parte da jornada.\""
a "\"Ele ainda é popular em Lux, então tê-lo ao meu lado pode ser bastante vantajoso. Depois disso, seguiremos caminhos separados.\""
m "\"Então, como o Alex vai se encaixar nisso tudo? O público sabe o que ele fez?\""
show ami embarrassed n with dis
a "\"Cass diz que enfatizar que Alex é seu animal de estimação vai reforçar a narrativa de que o sabotador foi conquistado.\""
m "\"Achei que você tivesse abolido o sistema de animais de estimação.\""
show ami serious c n with dis
a "\"Apenas mais um decreto sendo atrasado pelas travessuras de Cass. Será abolido assim que aquele maldito gato estiver fora do caminho.\""
show ami happy n with dis
a "\"Talvez eu deva apenas nos livrar do problema e surpreender Lux com uma execução pública. Eles ainda gostam desse tipo de espetáculo naquela região, sabia.\""
"Acho que estou começando a entender por que Neferu não contou ao Amicus sobre Alex."
"Tento mudar o tom da conversa para algo mais leve, no entanto."
m "\"Acho que você não o executaria, mesmo que não estivesse mudando o estilo diplomático de Adastra.\""
show ami smile c n with dis
a "\"Ah? Está insinuando que eu sou mole?\""
m "\"Bem, só acho que você se lembra de quando Alex era seu amigo e isso o impede. Além disso--\""
"Cutuco-o na barriga."
m "\"--você {i}é{/i} mole.\""
show ami surprised c n with dis
a "\"Mph!\""
"Ele grunhe e parece prestes a dizer algo, mas então olha para o lado, embaraçado, para o palácio."
m "\"Você está bem?\""
show ami embarrassed n with dis
a "\"Estou...\""
"Então ele respira fundo, como sempre faz antes de falar sobre algo que o está incomodando."
a "\"Falando em animais de estimação, lembre-se de que seu papel mudou e você deve se considerar um igual entre os irmãos.\""
m "\"Ok...\""
"Fecho os olhos, sem saber exatamente para onde isso está indo."
show ami embarrassed c n with dis
a "\"Aparência é uma parte muito importante da política, e talvez você deva considerar se portar de uma maneira mais... formal em comparação com quando estamos em privado.\""
"Penso nisso por um momento e então sinto meu rosto ficar um pouco quente, percebendo que Amicus está me dizendo para não constrangê-lo com demonstrações públicas de afeto."
"Instintivamente, quero ficar na defensiva, dizer a Amicus que isso é apenas mais um dos {i}seus{/i} problemas, mas..."
"Não, isso é realmente uma parte importante da política."
"É uma parte importante da vida, na verdade."
"Não posso simplesmente sair distribuindo beijos e esfregando o corpo do Amicus em público assim."
"Quero dizer, acho que já saberia disso, mas é estranho ouvir o Amicus dizer isso."
"Limpo a garganta, esperando não parecer tão vermelho quanto me sinto."
m "\"Ah, sim, claro. Lembro da minha postura formal.\""
"Entrelaço as mãos à minha frente."
a "\"Quando chegarmos às nossas vilas imperiais, será diferente.\""
show ami talking eyes n with dis
a "\"Você poderá fazer o que quiser comigo, então.\""
m "\"Sim, definitivamente.\""
show ami embarrassed n with dis
"Minha incapacidade de responder com brincadeiras como de costume não passa despercebida por Amicus, e o silêncio se torna constrangedor, algo que não acontece muito ultimamente."
"Não sei por que me sinto tão desanimado."
"Supondo que seja porque não havia realmente processado o fato até agora de que isso poderia ser um vislumbre da minha vida futura com Amicus."
"Muitas formalidades."
"Eu havia aceitado esse fato meses atrás, junto com o fato de que é muito provável que eu nunca me sinta em casa."
"Tanto em Adastra {i}quanto{/i} na Terra."
show ami sad n with dis
a "\"Sinto muito se disse algo idiota de novo. É só que, com a política, você precisa pensar em ambos--\""
m "\"Não, não, está tudo bem. É uma questão de hora e lugar. É só que... Bem, espero que eu seja capaz de fazer as coisas direito e que não estrague tudo e o envergonhe de alguma forma.\""
show ami surprised c n with dis
a "\"[mc], se você me envergonhar metade do que eu me envergonho sozinho, ficarei genuinamente impressionado.\""
"Acho que Amicus está subestimando minhas habilidades."
m "\"Bem, eu gostaria de não o envergonhar de forma alguma. Você já tem o suficiente para se preocupar sem eu causar problemas com coisas como aparência. Eu honestamente provavelmente deveria ficar na vila até você terminar todas as coisas importantes.\""
"Estou genuinamente tentando oferecer uma solução realista, mas não posso deixar de sentir que está soando como amargura."
"Amicus claramente pensa o mesmo."
show ami serious c n with dis
a "\"Ouça, não passamos pelo inferno e voltamos só para você ser escondido como um segredo vergonhoso. Você é importante, e não apenas para mim, mas para toda Adastra. Gafes públicas não são suficientes para mudar isso.\""
"Finalmente chegamos à margem nesse momento, e Amicus me ajuda a sair da embarcação."
scene bg lakeside with dissolve
m "\"Tudo bem, mas você sabe, em {i}Adastra: Uma História{/i}, eles falaram sobre alguns imperadores que não conseguiram se ajustar às formalidades do cargo e abdicaram, ou até mesmo, uh, 'descenderam à loucura' e se tornaram reis loucos. {i}Você{/i} está se saindo bem?\""
show ami talking eyes c n with dis
a "\"Por favor, [mc]. Minha mente não é tão fraca a ponto de não conseguir lidar com os deveres esperados de mim desde o nascimento.\""
show ami talking c n with dis
a "\"E você está se referindo ao Imperador Lucius, aquele que se casou e divorciou toda semana porque era impotente demais para ter filhos?\""
show ami happy eyes n with dis
"Amicus ri alto, e eu apenas o encaro."
m "\"Ok, em primeiro lugar, isso não tem nada a ver com fraqueza, e se você {i}realmente{/i} sentir que está desmoronando sob a pressão, me diga. Prometa-me isso.\""
"Meu tom faz com que a bravata de Amicus desabe como eu esperava."
show ami sad n with dis
a "\"Sim, claro, só tentando adicionar um toque de leveza--\""
m "\"Em segundo lugar, sim, eu estava falando de Lucius, e sim, ele era muito inseguro sobre seu pênis não funcionar a ponto de reviver a execução por imolação para punir qualquer um que ele achasse que pudesse ter mencionado isso.\""
show ami surprised n with dis
a "\"Ah, sim, acredito que qualquer coisa fálica era suficiente para deixá-lo furioso.\""
m "\"É uma história muito adastrana.\""
show ami embarrassed c n with dis
a "\"Embora eu não me lembre de menção a mortes por queima. Talvez você esteja confundindo a era dele com uma anterior?\""
"Abro a boca para negar, mas então lembro que ouvi aquela informação específica na réplica quêmica."
m "\"T-talvez, de qualquer forma, minha afirmação anterior permanece.\""
show ami eyes n with dis
a "\"Claro, e sinto que talvez isso também se aplique a você, no sentido de que está se sentindo um pouco sob pressão no momento?\""
"Eu suspiro."
m "\"Um pouco. Estou representando toda a humanidade, afinal.\""
"Amicus coloca o braço em volta de mim enquanto caminhamos em direção ao palácio."
show ami c n with dis
a "\"Confie em mim, tudo isso vai ficar mais fácil depois do seu primeiro dia. Vai acabar ficando um pouco chato depois disso.\""
stop music fadeout 5.0
m "\"Prefiro chato, ultimamente.\""
show ami talking eyes c n with dis
a "\"Eu também.\""

scene bg garden2 with fade
play music "sfx/birds.ogg" fadein 3.0
"Percebo que estamos bem atrasados, mesmo enquanto Amicus toma seu tempo para se preparar, incluindo encaixar outro banho no qual me puxa."
"Ele me garante que, devido à natureza semiformal da viagem, o cronograma não é tão rígido."
"Claro, isso não importa muito quando outros estão nos esperando."
"Outros como Virginia."
show vir annoyed at left with dis
"Ela nos encara enquanto nos aproximamos dela."
"A carruagem automática está atrás dela, com Neferu relaxando casualmente na frente, ocupando-se com seu tablet."
"Quando paramos diante dela, Amicus mantém o braço em volta de mim, ainda de bom humor."
"Virginia parece mexer a mandíbula por um momento antes de falar."
v "\"Você está {i}impressionantemente{/i} atrasado desta vez, Amicus, quase uma hora, de fato.\""
show ami at right with dis
a "\"Desculpe, Virginia, mas circunstâncias imprevistas exigiram que eu mergulhasse no lago. Então decidi que valia a pena estar atrasado para me lavar e me arrumar, para que eu pudesse estar apresentável--\""
show vir angry p with dis
v "\"Apenas entre na carruagem, Amicus.\""
show ami smile c with dis
a "\"Claro, querida irmã. Como você tem estado ultimamente? Não tivemos um momento para nos atualizar hoje.\""
"Eu teria chutado Amicus se estivéssemos sentados em uma mesa."
show vir annoyed p with dis
"Virginia apenas olha para o irmão de cima a baixo antes de se virar e entrar na carruagem."
hide vir with dis
v "\"Hoje fui lembrada de que a maioria das coisas não vale a pena, irmão.\""
show ami with dis
a "\"Fantástico!\""
hide ami with dis
"Amicus entra na carruagem e me oferece a pata, me puxando enquanto, ao mesmo tempo, a carruagem começa a se mover."
play background "sfx/chariot.ogg" fadein 5.0
show nef with dissolve
n "\"Suas táticas para antagonizá-la estão começando a funcionar, Virginia? Isso não é nada como você.\""
show vir at one with dissolve
v "\"Depende do dia, Neferu, embora eu deva admitir que esses dias em particular estão se tornando mais frequentes.\""
stop background fadeout 10.0
show ami eyes at thirteen behind nef with dis
"Olho para Amicus, mas ele apenas continua sorrindo, passando as garras rombudas pelo meu cabelo."
"Embora eu entenda e geralmente fique do lado de Virginia quando se trata do comportamento e da falta de pontualidade de Amicus--"
"--às vezes gosto quando ele consegue contrapor o humor sombrio dela com um excessivamente positivo."
scene bg forest with dissolve
stop music fadeout 5.0
play background "sfx/forest.ogg" fadein 5.0
"Viajamos em silêncio por um tempo, contentes em apenas apreciar a paisagem."
"Na metade do caminho, Neferu se vira."
show nef with dissolve
n "\"Você contou ao Amicus o que descobrimos na ilha, [mc]?\""
show ami serious at fourteen behind nef with dis
"Amicus solta uma respiração lenta, seu humor contente desaparecendo com ela."
show nef a with dis
n "\"Vou tomar isso como um sim.\""
m "\"Por que você não contou a ele?\""
n "\"Porque ele explode no tópico do Alex.\""
show nef
n "\"Você decidiu o que fazer?\""
a "\"Não.\""
n "\"Bem, você tem um conselheiro.\""
show vir at left with dis
v "\"Mate-o.\""
show nef sad with dis
n "\"Como é?\""
show vir serious p with dis
play music "music/beyondthestars1.ogg" fadein 10.0
v "\"Mate-o. O gato fez mais do que o suficiente para merecer. Não uma execução pública, entenda, mas sim um acidente.\""
"Repasso o que Virginia acabou de dizer em minha mente, não tenho certeza se estou entendendo corretamente."
v "\"Isso pelo menos privaria Omorfa de uma desculpa para a guerra. E se você realmente sentir que não tem opções em termos de deportação ou prisão, eu honestamente recomendo esse caminho. É absurdo que estejamos permitindo que um espião perigoso fique tão perto da família imperial.\""
v "\"É perigoso para toda Adastra.\""
"Eu me sinto desconfortável."
"Embora soubesse que Amicus não estava falando sério antes, Virginia parece estar mortalmente séria agora."
"Enquanto isso, Amicus não reage muito, continuando a encarar o banco à nossa frente enquanto acaricia meu cabelo."
show nef sad a with dis
n "\"A forma como você fala da morte tão casualmente para alguém que conhecemos é bastante perturbadora.\""
show vir annoyed with dis
v "\"Neferu, embora eu agradeça por me alertar sobre o problema, não estou com disposição para um sermão sobre a superioridade da moralidade quêmiana hoje.\""
show nef unsure f with dis
show vir annoyed p with dis
v "\"O maior problema com aquele gato é que ele voltará para Omorfa eventualmente com todo o conhecimento que obteve aqui em Adastra. Você não ouviu os rumores sobre o que os gatos estão planejando?\""
v "\"Eles afirmam que os Pais são maliciosos. Nem mesmo Adastra disse tais coisas abertamente.\""
"Se eu tivesse parado para pensar nisso, definitivamente teria assumido que Virginia gostaria que Alexios fosse executado."
"Mas sua ideia ecoa algo do passado que quero mencionar... mas não consigo."
"É uma sensação estranha, como se eu estivesse escolhendo ficar em silêncio porque não é meu lugar, mas também como se, se tentasse falar, talvez não conseguisse."
"Também sinto que estamos sendo observados."
"Isso não é uma sensação nova."
"É algo que tenho experimentado nos últimos meses."
"Na verdade, começou na universidade para a qual estamos indo agora, e tento forçar a imagem de uma estátua contorcida para o fundo da minha mente."
"Me encosto mais em Amicus."
stop background fadeout 10.0
n "\"--eles estão em turbulência política pela primeira vez em um milênio. Na minha opinião, é simplesmente um ajuste necessário com sua identidade e valores. Eles voltarão ao grupo como um povo mais forte, e nós os receberemos de volta.\""
v "\"O que seria um erro por parte dos Pais--\""
"Essa conversa sobre os Pais..."
v "\"--quando eles sabem do que os gatos são capazes.\""
"... me lembra que estou sob o controle deles de tantas maneiras, e agora sinto isso mais do que nunca."
"Abro a boca, sentindo a estrutura mecânica em minha garganta tensionar e protestar, travada como se tivesse parado de funcionar."
"Eles têm minha voz, e sei que estão usando isso contra mim agora."
"Agarro a coxa de Amicus e o ouço grunhir, então se vira para me olhar."
show ami embarrassed with dis
a "\"[mc], você está bem?\""
"Ele diz isso em voz baixa para não interromper Virginia e Neferu."
"{i}Eu não vou. Eu não vou. Eu não vou. Eu não vou... Ficar em silêncio!{/i}"
stop music fadeout 5.0
play sound "sfx/glitch.ogg"
m "\"IIIIIEEE NÃO VOU!\""
show ami shocked at jumping
show vir surprised
show nef shocked
with dis
"Amicus pula quando minha voz emite um som estranho e distorcido antes de voltar ao normal."
show ami surprised with dis
a "\"O que foi isso!? Você está bem?\""
m "\"Sim, desculpe, algo está errado com a máquina na garganta que os Pais colocaram em mim. Está funcionando agora, porém.\""
"Olho para Virginia."
m "\"Só queria dizer que sou completamente contra matar o Alex e preferiria que não falassem sobre isso na minha presença.\""
"Percebo então que estou ofegante."
"Eu planejava dizer isso de forma calma e firme, mas algo estava tentando me impedir, e isso me deixou ainda mais perturbado do que a conversa sobre assassinato."
show ami embarrassed
show nef sad
show vir sad
with dis
play music "sfx/forest.ogg" fadein 5.0
"Uma sensação estranha paira no ar, e não apenas a causada pelo meu estranho e distorcido surto."
"Em vez disso, algo parece diferente, como se eu tivesse me libertado de algo, como se tivesse saído de uma estrada na qual estava preso."
"É como se isso não devesse ter acontecido."
"E é como se eu estivesse livre."
show vir serious with dis
v "\"Amicus, seu amigo está psicologicamente são?\""
show ami serious with dis
a "\"Claro que está!\""
show ami embarrassed with dis
"Amicus olha para mim mais atentamente."
a "\"Você está bem, certo? Disse que sua modificação na garganta está com problemas?\""
"Esfrego minha garganta, finalmente recuperando o fôlego, e aceno rapidamente."
m "\"Sim... Sim, desculpe a todos. Algo travou e eu realmente queria dizer algo, então meio que explodiu.\""
show vir annoyed p with dis
v "\"Você realmente queria defender o gato?\""
show ami disappointed with dis
a "\"Ei, chega disso. Ele acabou de dizer que não queria se envolver nisso.\""
m "\"Só acho que deveríamos considerar quem mais planejou um assassinato e fez parecer um acidente. É uma situação diferente, sim, mas acho que somos um pouco melhores que ele e podemos encontrar uma maneira mais pacífica de resolver isso.\""
"Virginia mantém os olhos estreitados e não diz nada."
show nef with dis
n "\"Eu, por um lado, concordo. Também consideraria visitar os arquivos da universidade para ver se os Pais vão verificar seu dispositivo.\""
show nef smile eyes a with dis
n "\"Aquele som foi verdadeiramente sobrenatural.\""
m "\"Sim...\""
show ami embarrassed with dis
stop music fadeout 10.0
"Amicus apenas me olha com preocupação pelo resto da viagem, enquanto caímos em um silêncio muito desconfortável."
"Não me importo, porém."
"Estou tentando descobrir o que diabos acabou de acontecer e o que exatamente devo fazer a seguir."

scene bg uniday with fade
"As coisas continuam estranhas pelo resto da viagem até a universidade."
play music "music/memories.ogg" fadein 3.0
"Quando finalmente entramos, Cassius e Alex já estão lá, sentados no salão principal."
"Bem, Cassius está deitado em um banco, enquanto Alex está ao lado de uma criatura grande, uma que nunca vi antes."
"Ele é grande, mas a maioria dos alienígenas que vi também é."
"Sei que ele provavelmente é nosso guia, o urso que Amicus mencionou antes."
"Se algo, sua moda o denuncia como não sendo de Adastra."
"Não sei muito sobre eles ou sua cultura."
"Há pouca menção a eles em {i}Adastra: Uma História{/i}, provavelmente porque nunca guerrearam com os lobos."
"Isso me faz pensar se eles têm muito em comum."
"Alex está conversando com ele, e já posso dizer que a conversa é estranha antes mesmo de ouvir o que está sendo dito."
show ale tilt w at one
with dissolve
al "\"Pense desta forma; estou em uma cama de pregos cercada por chamas. Já estou em uma posição dolorosa, enquanto me dizem que tenho a escolha de sair dela... para uma posição pior.\""
al "\"Isso é realmente uma escolha ou apenas o manipulador usando uma situação ruim como desculpa para afirmar que lhe deu uma escolha?\""
show bja surprised with dis
unk "\"...\""
show bja eyes talk with dis
unk "\"Haha! Mas essa não é a escolha real agora, é?\""
"O urso parece notar nossa presença à medida que nos aproximamos."
show bja talk with dis
unk "\"Ah, Sua Majestade Imperial. É uma honra conhecê-lo.\""
"O urso se curva profundamente."
show ami at thirteen behind bja with dis
a "\"Saudações. Presumo que você seja nosso guia? Me disseram seu nome antes, mas parece que esqueci no caos do dia.\""
unk "\"Sem problemas. Eu sou Bjarni, e serei de fato seu guia. Falando em caos--\""
"Durante a troca, Alex estava se afastando casualmente em direção a Cassius, que ainda está deitado em um banco."
b "\"Seu pequeno amante do caos parece estar de volta aos velhos hábitos, se é que algum dia os abandonou. Tentando me virar contra os Pais logo antes da viagem. Ha!\""
show ale vshocked e at jumping
"Alexios congela quando percebe que está sendo abordado."
show ami serious c with dis
a "\"Chocante.\""
show ale flustered w with dis
al "\"E-eu estava apenas apresentando um experimento mental para ilustrar melhor uma ideia.\""
show bja eyes with dis
b "\"Que tópico para nossa primeira conversa, hein?\""
hide ale with dissolve
"Alexios sabiamente escolhe não responder e, em vez disso, volta para Cassius."
"Fica quieto por um momento, então o comportamento de Bjarni de repente fica sombrio."
show bja dark with dis
b "\"Sei que você já está ciente disso, mas fique de olho naquele gato. Ele é pura decepção.\""
show ami surprised with dis
a "\"Oh, er, eu planejo ficar.\""
show ami embarrassed with dis
show bja talk with dis
b "\"De qualquer forma, este é [mc]? Prazer em conhecê-lo também.\""
"Bjarni estende uma pata de uma forma tão familiar que instintivamente levanto minha mão."
"Ele então a pega e lentamente, mas deliberadamente, a aperta."
"Se não fosse por ele ser excessivamente cuidadoso com seus movimentos, eu nem teria percebido que isso é um aperto de mão e como é estranho que Bjarni pareça saber sobre isso."
"Amicus franze a testa com nossa troca, sem saber o que fazer."
show bja with dis
b "\"Parece que preciso trabalhar nisso, a julgar por sua reação.\""
a "\"Eh?\""
m "\"Foi um... aperto de mão?\""
show bja talk with dis
b "\"Definitivamente preciso trabalhar nisso, mas sim, a forma dominante de saudação física na Europa e na maioria da Terra, pelo que entendo. Achei que você soubesse disso, Amicus.\""
a "\"Ah, bem, eu sei bastante sobre a Terra, Europa e Itália, embora deva admitir que nunca fui saudado dessa forma.\""
"Amicus me dá um olhar como se isso fosse algo que eu deveria ter contado a ele."
m "\"É bem formal; não é algo que amigos fazem. Mas como você sabia disso, Bjarni?\""
show bja eyes with dis
b "\"Foi mostrado a mim durante o briefing para esta missão. Considerando que estamos nos preparando para integrar a humanidade em nossas interações diárias, achamos melhor começar o processo com você.\""
show bja with dis
b "\"Na verdade, o programa foi criado por você, Sua Majestade.\""
show ami surprised with dis
a "\"Ah, certo, certo.\""
show ami embarrassed with dis
a "\"Talvez eu deva passar por esses programas antes de implementá-los.\""
show bja eyes talk with dis
b "\"Bem, você está muito ocupado administrando a lua e tudo mais. Além disso, ter o primeiro humano em Adastra como seu companheiro é um pouco mais útil do que conhecer as complexidades do aperto de mão.\""
show ami with dis
a "\"É uma vantagem legal, sim.\""
"Amicus me puxa contra seu lado em um abraço rápido, o contato físico no estilo humano com o qual ele está mais familiarizado."
"Amicus então gesticula atrás de si."
a "\"De qualquer forma, esta é minha irmã e conselheira Virginia e Neferu, o embaixador não oficial de Khemia e o segundo filho do Faraó.\""
show vir at left with dissolve
show nef at negone with dis
v "\"Prazer.\""
n "\"Saudações.\""
stop music fadeout 5.0
"Há um pouco mais de conversa fiada, e fica claro que Bjarni realmente acha Neferu mais exótico do que eu, com uma longa tangente sobre o estado das coisas em Khemia."
"Finalmente, porém, Bjarni limpa a garganta."
hide vir
hide ami
hide nef
with dissolve
show bja with dis
play music "music/starship.ogg" fadein 5.0
b "\"Certo, vamos começar o briefing. Apenas uma visão geral do itinerário junto com detalhes gerais de cada local, principalmente para aqueles de nós que não são nativos da lua.\""
"O urso vasculha sua pessoa, alcançando um bolso invisível e puxando um tablet."
"Em vez do tablet mais comum em forma de bastão, é o tipo projetado para atuar como um projetor para demonstrações mais públicas."
show bja h with dis
b "\"Após discussões com o triunvirato e análises da situação política atual, decidimos visitar três locais.\""
show bja eyes talk h with dis
b "\"Primeiro, visitaremos a cidade de Lux. Oficialmente, é conhecida como a cidade fronteiriça de Adastra nas duras regiões montanhosas do extremo norte. Não oficialmente, é conhecida como a Cidade da Desafio.\""
b "\"Devido à popularidade de Cassius lá e à impopularidade geral de Amicus-- Perdão, Sua Majestade.\""
"Amicus acena com a pata como se não fosse um problema e pede para ele continuar."
b "\"--achamos melhor torná-la a principal prioridade. Lux é a maior produtora de recursos naturais em Adastra.\""
b "\"Uma olhada na história da cidade mostra que tempos de protestos frequentes, revoltas e greves, que muitas vezes interrompiam indústrias em toda Adastra, geralmente resultavam em imperadores fracassados.\""
b "\"Ao visitar Lux primeiro, mostramos que eles são nossa prioridade número um, e com Cassius presente, ele pode desempenhar um papel em ganhar mais apoio para Amicus.\""
"Bjarni faz uma pausa, olhando para Cassius, que parece estar olhando pela janela."
"Alex o cutuca sutilmente."
show cas at one behind bja with dissolve
c "\"Hm? Ah, sim, sim, de fato.\""
show ami pumped at right behind bja with dissolve
a "\"Você tem um discurso preparado, Cassius?\""
show cas annoyed with dis
c "\"Eu não preciso {i}preparar{/i} discursos, Amicus.\""
"Cassius aponta para a própria cabeça."
c "\"Ao contrário de você, eu sei o que eles querem, portanto o discurso já está escrito.\""
c "\"A questão é se você realmente fará o que quer que eu prometa a eles. Não vou mentir para um povo que já foi enganado por imperadores antigos... e novos.\""
"Há um silêncio constrangedor."
a "\"Falaremos mais sobre isso no caminho.\""
hide ami
hide cas
with dis
show bja eyes h with dis
"Bjarni limpa a garganta novamente."
b "\"De qualquer forma, Lux também solicitou que levemos um de seus triúnviros conosco para que ele possa desempenhar um papel nas conversas com outras regiões de Adastra.\""
show ami pumped at right behind bja with dis
a "\"Isso vai depender da habilidade dele no {i}pugnu{/i}.\""
m "\"O quê?\""
show bja eyes talk h with dis
b "\"Não é meu lugar, mas aconselho um pouco de cautela, pois o triúnviro que o desafiou já foi campeão de {i}pugnu{/i} da província.\""
show ami smile c with dis
a "\"O quê, 30 anos atrás, ou algo assim?\""
"Vejo meus sentimentos refletidos na postura de Virginia, que está com o rosto enterrado em uma pata."
show bja surprised h with dis
"Bjarni parece pensar, então diz rapidamente:"
b "\"Na verdade, se ele for facilmente derrotável, considere perder de propósito, pois não há dúvida de que uma derrota e, portanto, a perda de representação na turnê resultará em protestos e revoltas.\""
show ami surprised c with dis
a "\"Er...\""
show bja eyes talk h with dis
b "\"Prosseguindo--\""
"Amicus gagueja sobre suas palavras, mas não interrompe."
hide ami with dis
"Após este briefing, espero poder convencê-lo a resolver essa questão política de forma não violenta."
b "\"De lá, visitaremos a cidade de Lupas, considerada o epicentro cultural de Adastra. É aqui que você pode desempenhar um papel, [mc].\""
"Mudo de posição no banco em que estou sentado."
m "\"Oh?\""
"Olho para Amicus, franzindo a testa, mas ele parece tão confuso quanto eu."
show bja h with dis
b "\"Isso é um pouco de última hora, mas foi sugerido por seu triunvirato que ver e ouvir um humano falar pode ajudá-los a se ajustar à ideia de contato humano futuro.\""
m "\"Oh... Sim, claro. Eu poderia usar alguma orientação sobre o discurso, porém--\""
show bja eyes h with dis
b "\"Não tema. A maioria de nós não tem o dom da eloquência como Cassius, portanto, um redator de discursos já preparou suas observações.\""
"Isso me faz sentir melhor, mas fico surpreso quando é Neferu quem fala."
show nef angry a at left behind bja with dis
n "\"Perdoe-me por interromper, mas essa é uma decisão sábia? Porque, e me corrija se eu estiver errado, não é esse o centro de operações para o movimento separatista que está ganhando força atualmente?\""
n "\"Eu vi suas demonstrações, e embora seja principalmente imagens minhas sendo usadas para evocar sua raiva, a semelhança de [mc] também está sendo usada.\""
show bja surprised h with dis
b "\"Sério? Eu pensei que era apenas anti-quêmio? Assumi que seus sentimentos em relação aos primatas fossem mais cautelosos do que odiosos.\""
show ale serious at thirteen behind bja with dis
al "\"Não são os separatistas, mas sim um culto dedicado aos deuses lobos e à supremacia loba. Isso não quer dizer que não haja uma grande sobreposição; há. Mas os cultistas parecem muito estranhos para a marca separatista, então são mantidos separados.\""
m "\"O quê?\""
"Estou claramente completamente por fora, e sinto que Amicus deveria ter me contado isso."
show ami sad at right behind bja with dis
"Olho para ele acusadoramente, mas agora sua postura reflete a de Virginia não muito tempo atrás."
al "\"Na verdade, e sei que não é {i}meu{/i} lugar dizer isso, mas eu aconselharia [mc] a não visitar Lupas em seu estado atual. Este é o mesmo culto que tentou plantar uma bomba no Fórum, mas estragou tudo quando o responsável por plantá-la explodiu a si mesmo.\""
"Fica quieto por um momento."
"Então ele acrescenta, como se fosse um pensamento posterior:"
show ale tilt with dis
al "\"E eles queimaram um boneco de [mc] em sua demonstração mais recente.\""
"Olho para Amicus novamente, mas ele sussurra para mim."
show ami embarrassed with dis
a "\"Vamos conversar em privado após o briefing, certo?\""
"Eu apenas aceno, sem saber o que pensar agora."
hide ale
hide nef
hide ami
with dis
show bja frustrated h with dis
"Bjarni limpa a garganta mais uma vez."
show bja angry h with dis
b "\"Bem, isso é com você e o imperador, é claro, mas vou lembrar a todos que a segurança será hermética durante toda a jornada, então...\""
"Outra pausa constrangedora."
show bja talk h with dis
b "\"Prosseguindo.\""
b "\"O último destino é Ad Rotae; a cidade de entretenimento e resort de Adastra.\""
b "\"Este será um momento para relaxar após o trabalho nas cidades anteriores, embora, é claro, também seja uma oportunidade para mostrar ao público seu lado mais sábio. Para lembrá-los de que você não é um robô sem alma.\""
show bja with dis
"Bjarni guarda seu tablet e nos olha expectante."
stop music fadeout 10.0
b "\"Então esses são nossos principais objetivos para as próximas semanas. Alguma pergunta?\""
show vir at thirteen with dis
v "\"Quando é a partida?\""
show bja eyes talk with dis
b "\"Em cerca de uma hora. Teria sido agora... mas o atraso está forçando outra verificação de segurança. Peço desculpas por isso.\""
show vir annoyed p with dis
v "\"Não, isso é na verdade bom. Amicus, [mc]. Por favor, discutam o que acabamos de ouvir para que seja resolvido antes de partirmos.\""
show ami embarrassed at right behind bja with dis
a "\"Certo. Por aqui, [mc].\""
hide vir
hide bja
hide ami
with dissolve
"Sigo o lobo rigidamente por um corredor até parecer que estamos fora do alcance da audição."
show ami with dissolve
play music "music/memories.ogg" fadein 3.0
a "\"Acha que é privado o suficiente?\""
m "\"Acho. Por que precisa ser privado?\""
a "\"Bem, Virginia provavelmente só não quer ouvir, e sei que você prefere privacidade quando a conversa é sobre você.\""
m "\"Mas é sobre meus deveres, certo?\""
show ami eyes with dis
a "\"Você não tem deveres, [mc], apenas oportunidades se desejar aproveitá-las.\""
m "\"Digo, acho que deveria ser obrigado a fazer algumas coisas.\""
show ami serious with dis
a "\"Mas não se isso colocar sua vida em perigo.\""
m "\"Mas essa é a natureza do que estamos fazendo. E o que há com esse movimento separatista e um culto? Você me disse que isso era umas férias?\""
show ami embarrassed with dis
a "\"A maior parte é, mas haverá partes envolvendo a política usual de tudo isso.\""
m "\"Mas e o culto e os bonecos queimados de mim?\""
show ami surprised with dis
a "\"Sim, isso é algo de que eu não estava ciente. Precisarei investigar para garantir que aquele maldito gato não estava apenas mentindo para causar problemas novamente.\""
show ami serious with dis
a "\"Se for O caso, porém, eu concordaria que você não deveria fazer o discurso, apesar de ter segurança impenetrável. Eu preferiria que você nem entrasse na cidade, na verdade.\""
m "\"Se a segurança é hermética, acho que posso lidar com pelo menos fazer um breve discurso, para mostrar que sou um sábio que anda e fala, sabe?\""
"{i}Não faça o discurso.{/i}"
show ami surprised with dis
a "\"Ah--\""
m "\"Sim, eu-- eu quero fazer o discurso.\""
"Amicus permanece congelado, e é claro que ele quer desesperadamente dizer não."
show ami embarrassed with dis
"Amicus percebe Neferu no corredor, espreitando enquanto olha para as estátuas, provavelmente tentando ouvir pedaços de nossa conversa."
hide ami with dissolve
stop music fadeout 5.0
"O lobo pega minha mão e me guia pela porta mais próxima ao nosso lado."
"É um átrio, com o sol atualmente em um ângulo que não permite muita luz."
"Amicus me leva para um canto, então respira fundo."
play music "music/eightyears.ogg" fadein 2.0
scene atrium1 with dissolve
a "\"Certo, sei que fiz promessas de confiar em você, que nunca vou dizer o que fazer--\""
m "\"Mas?\""
a "\"Mas ainda vou aconselhá-lo, e é apenas... Você já teve um PÉSSIMO pressentimento sobre algo, que algo simplesmente não é uma boa ideia.\""
m "\"Sim. O tempo todo, Amicus."
a "\"Eee... Como você se sente sobre isso, exatamente?\""
"Amicus me olha atentamente."
"{i}Não faça o discurso.{/i}"
"Encolho os ombros como se estivesse fisicamente sentindo algo."
m "\"Bem, muito bem, eu acho.\""
"{i}O quê?{/i}"
a "\"Uh...\""
m "\"Tudo bem, pronto para voltar?\""
a "\"...\""
a "\"Não.\""
"Cruzo os braços, inclinando a cabeça em um gesto de \"eu sabia que você faria isso\"."
scene atrium2 with dis
"Amicus bufa de irritação."
"Estou provocando Amicus, mas acho que sei do que ele está falando, se esse {i}pressentimento{/i} for o que estou {i}ouvindo{/i} e se for o mesmo para ele."
a "\"É muito difícil explicar, [mc]. Sei que deve parecer terrível, a maneira como prometi a você total agência, mas questiono todas as suas decisões--\""
a "\"--mas isso é honestamente diferente. Estou genuinamente preocupado com sua segurança e acho que seria irresponsável da minha parte deixá-lo entrar em uma situação que você não entende completamente.\""
m "\"Digo, você poderia ter me contado sobre algumas dessas coisas.\""
a "\"Sim, e admito que há coisas que deveria ter dito, mas sei que você mesmo está sob uma grande pressão de maneiras diferentes.\""
a "\"E suponho que devo admitir que algumas coisas eu simplesmente escolho não contar porque só trariam miséria para você. Eu certamente teria contado sobre o que está acontecendo em Lupas se soubesse sobre o discurso e a interação direta com seu povo.\""
a "\"Eu só quero que você se sinta capaz de relaxar em algum momento... E isso ia acontecer até aquele maldito urso mudar o cronograma.\""
m "\"Ele está apenas fazendo seu trabalho, e eu deveria estar fazendo o meu.\""
a "\"Ouça--\""
n "\"Desculpe, estou interrompendo?\""
scene atrium3 with dis
"Olhamos para cima e vemos Neferu relaxando na porta da maneira mais provocante que já vi."
"O que definitivamente é dizer muito."
scene atrium4 with dis
a "\"Que diabos? Não faça perguntas idiotas só para fazer uma entrada, e essa é uma conversa privada. Você não ouviu Virginia?\""
n "\"Sim, mas ela me pediu para ouvir e oferecer meu próprio conselho para garantir que você não crie mais conflitos no caminho.\""
m "\"Sobre fazer o discurso. Eu já tomei minha decisão.\""
"Neferu caminha até o sofá e se acomoda casualmente."
scene atrium5 with dis
n "\"Ele mencionou o histórico de assassinatos de Lupas? Eles são muito bons em matar aqueles que são instrumentais para a desradicalização e alianças.\""
"Amicus olha para mim, respirando com dificuldade."
a "\"É-- É difícil lembrar de cada coisa que ele precisa saber. E--\""
"Amicus está gaguejando e tropeçando em palavras como nunca vi antes."
m "\"Ei, está tudo bem. Você quer evitar as coisas que vão me preocupar. Eu faço isso com você também, mas--\""
m "\"--essas são coisas que eu preciso saber agora. Eu sou parte de tudo isso, e isso vai trazer muita responsabilidade com isso.\""
scene atrium6 with dis
n "\"Ele está certo, sabe. Você deveria pelo menos ter mencionado o movimento porque isso o envolve diretamente.\""
a "\"Isso não o envolve; {i}eles{/i} estão o envolvendo contra a vontade dele.\""
"Amicus rosna de frustração."
a "\"Malditos sejam todos!\""
m "\"Bem, não é realmente sobre mim, é? Eu sou apenas uma ideia para eles, e eles querem destruir essa ideia. Então fazer um discurso pode ser uma boa ideia.\""
m "\"É como Cassius em Lux; eu poderia influenciar as pessoas menos radicais se elas puderem ver que eu não sou... sei lá, um monstro terrível.\""
scene atrium7 with dis
"Amicus suspira profundamente mais uma vez, fechando os olhos e mantendo-os fechados."
"É quase como se ele estivesse ouvindo algo."
"Orientação parental, talvez?"
m "\"Você acha que eu deveria fazer um discurso, Neferu?\""
scene atrium8 with dis
n "\"Hmm...\""
"O chacal parece pensar por um momento."
n "\"Tenho duas respostas;\""
n "\"Como seu amigo, não. Acho que haveria muito risco para eu me sentir confortável com você em público em qualquer lugar de Lupas.\""
"Amicus acena silenciosamente."
n "\"Do ponto de vista de um aliado político, porém, acho que seria vantajoso para nós dois, e eu também farei aparições públicas lá.\""
"Isso na verdade me faz sentir um pouco melhor."
"{i}Não faça o discurso.{/i}"
m "\"Então... eu vou fazer. Este é meu dever."
"{i}Vestígios de Meera...{/i}"
"Amicus fica quieto por um tempo, então acena."
a "\"Certo.\""
scene atrium9 with dis
a "\"Embora eu vá ter uma palavra com aquele urso. Não confio na maneira como ele nos informou. Eu sou o imperador e deveria ser informado sobre essas coisas antes de anunciar de maneiras que me envergonhem completamente!\""
scene atrium10 with dis
stop music fadeout 10.0
b "\"Desculpe, só vim mencionar que a verificação de segurança está adiantada. Havia algo que você queria me dizer?\""
a "\"Ah! Er... Só que, uh--\""
m "\"Deixe-me dar-lhe privacidade. Vou visitar os banheiros\""
"Bjarni entra enquanto eu saio, deixando Amicus lidar com aquela situação em particular."
scene bg uniday with dissolve
"Minha mente está confusa no momento, tentando descobrir várias coisas de uma vez."
"Algo sobre a maneira como os Pais... Ou o que quer que fosse, tentou manipular minha voz, bem--"
"--me deixou com raiva, e ter essa voz de volta quando as coisas estão ficando perigosas novamente é demais."
"Só vai me levar a cenários que eles \"não planejaram\", ou algo assim."
"Eles não sabem o que estão fazendo, e para eles começarem a fazer esses avisos sinistros novamente sobre algo que poderia ser útil para todos nós está me deixando com mais raiva."
"Vejo Alex no salão principal, de lado, lendo as inscrições nas estátuas."
"Vou contar a ele sobre o que está sendo dito sobre ele, mesmo que seja apenas porque sinto que é a coisa certa a fazer."
"E porque os Pais tentaram me silenciar por falar sobre isso."
"E enquanto caminho em sua direção, o tempo para."
scene bg space with slow_dissolve
"Há uma certa satisfação que sinto, junto com uma boa dose de medo."
"Mas não vou deixá-los estragar as coisas uma segunda vez."
"Quero respostas reais antes que o plano deles seja colocado em ação."
scene monitor with slow_dissolve
mon "\"Bem, você mudou.\""
"Você poderia dizer que isso é exatamente o que eu queria que acontecesse."
"Mas duvido que consiga o que quero deles com facilidade."
jump a1s3